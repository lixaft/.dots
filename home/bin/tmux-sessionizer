#!/usr/bin/env nu

def find_projects [] {
    ["~/*" "~/dev/*"]
    | par-each {|p| glob --no-symlink --no-file $p}
    | flatten
    | where {|p| [".git" ".jj" "HEAD"] | any {|f| $p | path join $f | path exists}}
    | sort
}

def goto_session [name: string, directory: string, cmd: list = []] {
    try {
        tmux has-session -t $name e> /dev/null
    } catch {
        tmux new-session -d -s $name -c $directory -- ...$cmd
    }

    if ($env.TMUX == null) {
        tmux attach-session -t $name
    } else {
        tmux switch-client -t $name
    }
}

def main [
    path?: string  # Path to use as the root of the session
] {
    mut final_path = $path

    if ($path == null) {
        $final_path = find_projects | str join "\n" | fzf
    }

    if not ($final_path | path exists) {
        error make --unspanned { msg: "path does not exists" }
    }

    match ($final_path | path type) {
        "dir" => {
            let dir = $final_path
            let name = $final_path | path basename | str replace "." ""
            goto_session $name $dir
        }
        "file" => {
            let dir = $final_path | path dirname
            let file = $final_path | path basename
            let name = $final_path | path parse | get stem | str replace "." ""
            goto_session $name $dir [$env.EDITOR $file]
        }
    }
}
